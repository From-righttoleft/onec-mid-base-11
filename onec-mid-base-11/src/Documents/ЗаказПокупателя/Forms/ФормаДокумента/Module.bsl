
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
    // СтандартныеПодсистемы.ПодключаемыеКоманды
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
    // Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
    ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыТовары

&НаКлиенте
Процедура ТоварыКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура ТоварыПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

&НаКлиенте
Процедура ТоварыСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Товары.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыУслуги

&НаКлиенте
Процедура УслугиКоличествоПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиЦенаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиСкидкаПриИзменении(Элемент)
	
	ТекущиеДанные = Элементы.Услуги.ТекущиеДанные;
	
	РассчитатьСуммуСтроки(ТекущиеДанные);
	
КонецПроцедуры

&НаКлиенте
Процедура УслугиПриИзменении(Элемент)
	РассчитатьСуммуДокумента();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РассчитатьСуммуСтроки(ТекущиеДанные)
	
		//++ ООО АБС, Лесков Р.А., 24.08.24, отключить стандартное расчет 
	//КоэффициентСкидки = 1 - ТекущиеДанные.Скидка / 100; 
	//--
	
	//++ ООО АБС, Лесков Р.А., 24.08.24, новый расчет коэффициента скидки 
	КоэффициентСкидки = 1 - (Объект.Д_СогласованнаяСкидка + ТекущиеДанные.Скидка) / 100; 
	
	Если КоэффициентСкидки < 0 Тогда  
		
		Сообщение = Новый СообщениеПользователю();
		Сообщение.Текст = СтрШаблон("Суммарная скидка по позиции %1 превышает 100%%", ТекущиеДанные.Номенклатура);
		Сообщение.Сообщить();
		
		КоэффициентСкидки = 0;
		
	КонецЕсли;
	//--
	
	ТекущиеДанные.Сумма = ТекущиеДанные.Цена * ТекущиеДанные.Количество * КоэффициентСкидки;
	
КонецПроцедуры

&НаКлиенте
Процедура РассчитатьСуммуДокумента()
	
	Объект.СуммаДокумента = Объект.Товары.Итог("Сумма") + Объект.Услуги.Итог("Сумма");
	
КонецПроцедуры

#Область ПодключаемыеКоманды

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры

&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти

#КонецОбласти

#Область ДобавленияИзменения  

&НаКлиенте
Процедура КомандаПересчитатьТаблицу(Команда) 
	
	//++ ООО АБС, Лесков Р.А., 24.08.24 
	
	ТекстВопроса = "Пересчитать сумму товаров с учетом процента скидки?";
	
	ЗадатьВопросАсинх(ТекстВопроса);
	
	//--
	
КонецПроцедуры 

&НаКлиенте
Процедура Д_ВызватьПересчетСтроки()
	
	//++ ООО АБС, Лесков Р.А., 24.08.24, Формирует данные для пересчета стоимости со скидкой 
	
	ДанныеТабличныхЧастей = Новый Массив;
	
	ДанныеТабличныхЧастей.Добавить(Объект.Товары);
	
	ДанныеТабличныхЧастей.Добавить(Объект.Услуги);
	
	Для Каждого Таблица Из ДанныеТабличныхЧастей Цикл  
		
		Если Таблица.Количество() = 0 Тогда
			Продолжить;
		КонецЕсли;
		
		Для Каждого ТекущиеДанные Из Таблица Цикл
			
			РассчитатьСуммуСтроки(ТекущиеДанные);
			
		КонецЦикла;
		
	КонецЦикла; 
	
	РассчитатьСуммуДокумента();
	//--
КонецПроцедуры  

&НаКлиенте
Процедура Д_СогласованнаяСкидкаПриИзменении(Элемент) 
	
	//++ ООО АБС, Лесков Р.А., 24.08.24
	
	ТекстВопроса = "Процент скидки изменился. Пересчитать сумму товаров и услуг?";
	
	ЗадатьВопросАсинх(ТекстВопроса); 
	
	//--
	
КонецПроцедуры

&НаКлиенте
Асинх Процедура ЗадатьВопросАсинх(ТекстВопроса) 
	
	//++ ООО АБС, Лесков Р.А., 24.08.24, вопрос пользователю на подтверждение действия 
	
	Режим = РежимДиалогаВопрос.ДаНет;
	
	Ответ = Ждать ВопросАсинх(ТекстВопроса, Режим);
	
	Если Ответ = КодВозвратаДиалога.Нет Тогда
		Возврат;
	КонецЕсли;
	
	Д_ВызватьПересчетСтроки();
	
	//--
			
КонецПроцедуры

#КонецОбласти
